#!/usr/bin/gnuplot5 -c

print "plotting data : ", ARG1
print "output to     : ", ARG2

if (ARGC < 2) {
	print "USAGE: ", ARG0, " <input CSV format> <output PNG file> [Title]"
	exit
}

set title ARG3 noenhanced

yoffset = 2147483648
ydatarange = 2147483648

set terminal unknown

set key inside left

set xtic auto                          # set xtics automatically
set ytic auto                          # set ytics automatically

set xlabel "CPU Cycle"
set xtic 100000000

set ylabel "Physical Address"
set yr [0:ydatarange]
set ytic ydatarange / 8
set format y "0x%08x"

set datafile separator ","

plot ARG1 using 1:($3 == 0 ? $2 - yoffset : 1/0) title "LOAD"  with points pt 2, \
     ARG1 using 1:($3 == 1 ? $2 - yoffset : 1/0) title "STORE" with points pt 11

xdim = 250 + (GPVAL_DATA_X_MAX / 1000000)
ydim = 750

set terminal png size xdim, ydim
set output ARG2
set size ratio ydim / xdim

replot
